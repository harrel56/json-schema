plugins {
    id 'java-test-fixtures'
}

apply from: 'testing.gradle'

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    options.compilerArgs.addAll(['--release', '8'])
}

java {
    registerFeature('format') {
        usingSourceSet(sourceSets.main)
    }
}

def jsonProviders = [
        [id   : 'jackson', additionalVersions: ['2.2.0'],
         group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.20.1'],
        [id   : 'gson', additionalVersions: ['2.3'],
         group: 'com.google.code.gson', name: 'gson', version: '2.13.2'],
        [id   : 'orgJson', additionalVersions: ['20201115'],
         group: 'org.json', name: 'json', version: '20251224'],
        [id   : 'kotlinxJson', additionalVersions: ['1.0.0'],
         group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-json', version: '1.9.0'],
        [id   : 'jakartaJson', additionalVersions: [],
         group: 'jakarta.json', name: 'jakarta.json-api', version: '2.1.3'],
        [id   : 'jettison', additionalVersions: ['1.5.0'],
         group: 'org.codehaus.jettison', name: 'jettison', version: '1.5.4'],
        [id   : 'jsonSmart', additionalVersions: ['1.1'],
         group: 'net.minidev', name: 'json-smart', version: '2.6.0'],
        [id   : 'snakeYaml', additionalVersions: ['1.26', '2.0'],
         group: 'org.yaml', name: 'snakeyaml', version: '2.5'],
]

setupJava8Test(jsonProviders[0])
jsonProviders.forEach {
    java.registerFeature(it.id, {
        usingSourceSet(sourceSets.main)
    })
    dependencies.add("${it.id}Implementation", [group: it.group, name: it.name, version: it.version])
    setupProviderTest(it)
}

dependencies {
    formatImplementation 'com.sanctionco.jmail:jmail:2.1.0'

    jakartaJsonTestImplementation 'org.eclipse.parsson:parsson:1.1.7'
    testRuntimeOnly 'org.eclipse.parsson:parsson:1.1.7'

    testFixturesApi 'org.junit.platform:junit-platform-launcher:1.14.2'
    testFixturesApi 'org.junit.jupiter:junit-jupiter-api:5.14.2'
    testFixturesApi 'org.junit.jupiter:junit-jupiter:5.14.2'
    testFixturesApi 'org.assertj:assertj-core:3.27.6'
    testFixturesApi 'org.mockito:mockito-core:5.21.0'
    testFixturesApi 'com.sanctionco.jmail:jmail:2.1.0'
    testFixturesRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.14.2'
}

processTestFixturesResources {
    def fileNames = []
    eachFile {
        fileNames.add "/${it.path}"
    }
    doLast {
        def indexFile = new File(destinationDir, 'files.index')
        indexFile.text = fileNames.join(System.lineSeparator())
    }
}

// don't publish test-fixtures jar
components.java.withVariantsFromConfiguration(configurations.testFixturesApiElements) {
    skip()
}
components.java.withVariantsFromConfiguration(configurations.testFixturesRuntimeElements) {
    skip()
}

publishing {
    publications.withType(MavenPublication).configureEach {
        pom.name = 'json-schema'
        pom.description = 'Library for JSON schema validation'
    }
}

sonar {
    properties {
        property 'sonar.projectKey', 'harrel56_json-schema'
        property 'sonar.organization', 'harrel56'
        property 'sonar.host.url', 'https://sonarcloud.io'

        property 'sonar.java.source', '8'
        property 'sonar.java.target', '8'
    }
}