group 'dev.harrel.json.providers'

compileJava {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    options.compilerArgs.addAll(['--release', '17'])
}

dependencies {
    implementation project(':json-schema')
    implementation 'tools.jackson.core:jackson-databind:3.0.2'

    testImplementation testFixtures(project(':json-schema'))
}

jacocoTestReport {
    executionData fileTree(project.rootDir.absolutePath).include("build/jacoco/*.exec")

    reports.html.required = false
    reports.csv.required = true
    reports.xml.required = true
}

sonar {
    properties {
        property 'sonar.projectKey', 'harrel56_providers_jackson3'
        property 'sonar.organization', 'harrel56'
        property 'sonar.host.url', 'https://sonarcloud.io'

        property 'sonar.java.source', '17'
        property 'sonar.java.target', '17'
    }
}

publishing {
    repositories {
        maven {
            name = 'reposilite'
            url = findProperty('reposiliteSnapshots')
            credentials {
                username = findProperty('reposiliteUsername')
                password = findProperty('reposilitePassword')
            }
        }
        maven {
            name = 'sonatype'
            url = findProperty('sonatypeRepo')
            credentials {
                username = findProperty('sonatypeUsername')
                password = findProperty('sonatypePassword')
            }
        }
    }
    publications {
        local(MavenPublication) {
            from components.java
        }
        sonatype(MavenPublication) {
            from components.java
            pom {
                name = 'Jackson 3.x JSON provider'
                description = 'A JSON provider for new Jackson versions (3.x)'
                url = 'https://github.com/harrel56/json-schema'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/harrel56/json-schema/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'harrel56'
                        name = 'Harrel'
                        url = 'https://harrel.dev'
                    }
                }
                scm {
                    url = 'https://github.com/harrel56/json-schema'
                    connection = 'scm:git:git://github.com/harrel56/json-schema.git'
                    developerConnection = 'scm:git:git://github.com/harrel56/json-schema.git'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/harrel56/json-schema/issues'
                }
            }
        }
    }
}

signing {
    if (findProperty('signingKey') != null) {
        def decodedKey = new String(findProperty('signingKey').decodeBase64())
        useInMemoryPgpKeys(decodedKey, findProperty('signingPassword'))
    }
    sign publishing.publications.sonatype
}