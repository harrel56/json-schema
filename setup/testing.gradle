// todo this should be removed as it will be equivalent of `test` task
tasks.register('genericTests', Test) {
    description = 'Runs all tests that are not related to specific JSON providers (no specification tests)'
    useJUnitPlatform()
    group = 'verification'

    filter {
        excludeTestsMatching 'dev.harrel.jsonschema.providers.*'
    }
}
tasks.check.dependsOn genericTests

/**
 * Each provider gets SourceSet with Test task.
 * Each additional version of provider reuses created SourceSet and creates Configuration and Test task for itself.
 */
ext.setupProviderTest = (Map data) -> {
    def taskName = "${data.id}Test"
    def configName = "${taskName}Implementation"
    sourceSets.create(taskName) {
        java {
            srcDir file("src/integration/${taskName}")
            compileClasspath += sourceSets.main.output + sourceSets.test.output
            runtimeClasspath += sourceSets.main.output + sourceSets.test.output
        }
    }
    def sourceSet = sourceSets.getByName(taskName)
    configurations.getByName(configName).extendsFrom configurations.testImplementation, configurations.formatImplementation
    dependencies.add(configName, [group: data.group, name: data.name, version: data.version])
    tasks.register(taskName, Test) {
        useJUnitPlatform()
        group = 'provider'
        testClassesDirs = sourceSet.output.classesDirs
        classpath = sourceSet.runtimeClasspath
//        javaLauncher = javaToolchains.launcherFor {
//            languageVersion = JavaLanguageVersion.of(8)
//        }
    }
    tasks.check.dependsOn taskName

    (data.additionalVersions as List<String>).forEach {
        def additionalTaskName = "${data.id}${it.replace('.', '_')}Test"
        def additionalConfigName = "${additionalTaskName}Implementation"
        configurations.create(additionalConfigName)
        configurations.getByName(additionalConfigName).extendsFrom configurations.testImplementation, configurations.formatImplementation
        dependencies.add(additionalConfigName, [group: data.group, name: data.name, version: it])
        tasks.register(additionalTaskName, Test) {
            useJUnitPlatform()
            group = 'provider'
            testClassesDirs = sourceSet.output.classesDirs
            classpath = configurations.getByName(additionalConfigName).getIncoming().getFiles() + sourceSet.output + sourceSets.test.output + sourceSets.main.output
//        javaLauncher = javaToolchains.launcherFor {
//            languageVersion = JavaLanguageVersion.of(8)
//        }
        }
        tasks.check.dependsOn additionalTaskName
    }
}