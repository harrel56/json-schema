tasks.register('genericTests', Test) {
    description = 'Runs all tests that are not related to specific JSON providers (no specification tests)'
    useJUnitPlatform()
    group = 'verification'

    filter {
        excludeTestsMatching 'dev.harrel.jsonschema.providers.*'
    }
}
tasks.check.dependsOn genericTests

ext.setupProviderTest = (Map data) -> {
    def taskName = "${data.id}Test"
    def configName = "${taskName}Implementation"
    sourceSets.create(taskName) {
        java {
            srcDir file("src/provider-test/${data.id}")
            compileClasspath += sourceSets.main.output + sourceSets.test.output
            runtimeClasspath += sourceSets.main.output + sourceSets.test.output
        }
    }
    configurations.getByName(configName).extendsFrom configurations.testImplementation, configurations.formatImplementation
    dependencies.add(configName, [group: data.group, name: data.name, version: data.version])
    tasks.register(taskName, Test) {
        useJUnitPlatform()
        group = 'provider'
        testClassesDirs = sourceSets.getByName(taskName).output.classesDirs
        classpath = sourceSets.getByName(taskName).runtimeClasspath
//        javaLauncher = javaToolchains.launcherFor {
//            languageVersion = JavaLanguageVersion.of(8)
//        }
    }
    tasks.check.dependsOn taskName

    (data.additionalVersions as List<String>).forEach {
        def taskName2 = "${data.id}${it.replace('.', '_')}Test"
        def configName2 = "${taskName2}Implementation"
        configurations.create(configName2)
        configurations.getByName(configName2).extendsFrom configurations.testImplementation, configurations.formatImplementation
        dependencies.add(configName2, [group: data.group, name: data.name, version: it])
        tasks.register(taskName2, Test) {
            useJUnitPlatform()
            group = 'provider'
            testClassesDirs = sourceSets.getByName(taskName).output.classesDirs
            classpath = configurations.getByName(configName2).getIncoming().getFiles() + sourceSets.getByName(taskName).output + sourceSets.test.output + sourceSets.main.output
//    classpath = configurations.getByName('gson2TestImplementation').getIncoming().getFiles() + sourceSets.main.output + sourceSets.test.output
//        javaLauncher = javaToolchains.launcherFor {
//            languageVersion = JavaLanguageVersion.of(8)
//        }
        }
        tasks.check.dependsOn taskName2
    }
}