import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'org.sonarqube' version '7.0.1.6134' apply false
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'jacoco'
    apply plugin: 'org.sonarqube'

    group 'dev.harrel'
    version '1.9.0-beta.1'

    repositories {
        mavenCentral()
    }

    java {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
        withSourcesJar()
        withJavadocJar()
        javadoc.options.addStringOption('Xdoclint:none', '-quiet')
    }

    test {
        useJUnitPlatform()
        jvmArgs("-XX:+EnableDynamicAgentLoading")
    }

    jacocoTestReport {
        executionData fileTree(project.layout.buildDirectory).include("jacoco/*.exec")

        reports.html.required = false
        reports.csv.required = true
        reports.xml.required = true
    }

    publishing {
        repositories {
            maven {
                name = 'reposilite'
                url = findProperty('reposiliteSnapshots')
                credentials {
                    username = findProperty('reposiliteUsername')
                    password = findProperty('reposilitePassword')
                }
            }
            maven {
                name = 'sonatype'
                url = findProperty('sonatypeRepo')
                credentials {
                    username = findProperty('sonatypeUsername')
                    password = findProperty('sonatypePassword')
                }
            }
        }
        publications {
            sonatype(MavenPublication) {
                from components.java
                pom {
                    url = 'https://github.com/harrel56/json-schema'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://github.com/harrel56/json-schema/blob/master/LICENSE'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = 'harrel56'
                            name = 'Harrel'
                            url = 'https://harrel.dev'
                        }
                    }
                    scm {
                        url = 'https://github.com/harrel56/json-schema'
                        connection = 'scm:git:git://github.com/harrel56/json-schema.git'
                        developerConnection = 'scm:git:git://github.com/harrel56/json-schema.git'
                    }
                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/harrel56/json-schema/issues'
                    }
                }
            }
        }
    }

//    signing {
//        if (findProperty('signingKey') != null) {
//            def decodedKey = new String(findProperty('signingKey').decodeBase64())
//            useInMemoryPgpKeys(decodedKey, findProperty('signingPassword'))
//        }
//        sign publishing.publications.sonatype
//    }

    tasks.withType(Test).configureEach {
        testLogging {
            events TestLogEvent.SKIPPED, TestLogEvent.FAILED, TestLogEvent.STANDARD_OUT, TestLogEvent.STANDARD_ERROR
            exceptionFormat 'full'
        }
        afterSuite { desc, result ->
            if (!desc.parent) {
                println("${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)")
            }
        }
    }
}

/* For GH actions */
tasks.register('integrationTasksArray') {
    def taskNames = project('json-schema').tasks.findAll { it.group == 'integration' }.name
    def submoduleTests = subprojects.findAll { it.name != 'json-schema' }.collect { "${it.name}:test" }
    print groovy.json.JsonOutput.toJson(submoduleTests + taskNames)
}