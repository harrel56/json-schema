plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id "org.sonarqube" version "5.0.0.4638"
}

apply from: 'setup/publishing.gradle'
apply from: 'setup/reporting.gradle'
apply from: 'setup/testing.gradle'

group 'dev.harrel'
version '1.6.0'

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    options.compilerArgs.addAll(['--release', '8'])
}

java {
    sourceCompatibility JavaVersion.VERSION_21
    targetCompatibility JavaVersion.VERSION_21
    withSourcesJar()
    withJavadocJar()
    javadoc.options.addStringOption('Xdoclint:none', '-quiet')

    registerFeature('format') {
        usingSourceSet(sourceSets.main)
    }
}

repositories {
    mavenCentral()
}

def jsonProviders = [
        jackson    : 'com.fasterxml.jackson.core:jackson-databind:2.17.1',
        gson       : 'com.google.code.gson:gson:2.10.1',
        orgJson    : 'org.json:json:20240303',
        jettison   : 'org.codehaus.jettison:jettison:1.5.4',
        jakartaJson: 'jakarta.json:jakarta.json-api:2.1.3',
        jsonSmart  : 'net.minidev:json-smart:2.5.1',
        snakeYaml  : 'org.yaml:snakeyaml:2.2'
]

jsonProviders.entrySet().forEach {
    java.registerFeature(it.key, {
        usingSourceSet(sourceSets.main)
    })
    dependencies.add("${it.key}Implementation", it.value)
    setupProviderTest(it.key, it.value)
}

dependencies {
    formatImplementation 'com.sanctionco.jmail:jmail:1.6.2'

    testImplementation 'org.eclipse.parsson:parsson:1.1.6'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'org.assertj:assertj-core:3.25.3'
    testImplementation 'org.mockito:mockito-core:5.12.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
}

test {
    useJUnitPlatform()
    jvmArgs("-XX:+EnableDynamicAgentLoading")
}