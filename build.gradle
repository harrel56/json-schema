plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id "org.sonarqube" version "6.0.0.5145"
}

apply from: 'setup/publishing.gradle'
apply from: 'setup/reporting.gradle'
apply from: 'setup/testing.gradle'

group 'dev.harrel'
version '1.7.1'

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    options.compilerArgs.addAll(['--release', '8'])
}

java {
    sourceCompatibility JavaVersion.VERSION_21
    targetCompatibility JavaVersion.VERSION_21
    withSourcesJar()
    withJavadocJar()
    javadoc.options.addStringOption('Xdoclint:none', '-quiet')

    registerFeature('format') {
        usingSourceSet(sourceSets.main)
    }
}

repositories {
    mavenCentral()
}

def jsonProviders = [
        jackson    : 'com.fasterxml.jackson.core:jackson-databind:2.18.1',
        gson       : 'com.google.code.gson:gson:2.11.0',
        orgJson    : 'org.json:json:20240303',
        kotlinxJson: 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.7.3',
        jakartaJson: 'jakarta.json:jakarta.json-api:2.1.3',
        jettison   : 'org.codehaus.jettison:jettison:1.5.4',
        jsonSmart  : 'net.minidev:json-smart:2.5.1',
        snakeYaml  : 'org.yaml:snakeyaml:2.3'
]

jsonProviders.entrySet().forEach {
    java.registerFeature(it.key, {
        usingSourceSet(sourceSets.main)
    })
    dependencies.add("${it.key}Implementation", it.value)
    setupProviderTest(it.key, it.value)
}

dependencies {
    formatImplementation 'com.sanctionco.jmail:jmail:1.6.3'

    testImplementation 'org.eclipse.parsson:parsson:1.1.7'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.3'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.3'
    testImplementation 'org.assertj:assertj-core:3.26.3'
    testImplementation 'org.mockito:mockito-core:5.14.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.3'
}

test {
    useJUnitPlatform()
    jvmArgs("-XX:+EnableDynamicAgentLoading")
}